# -*- mode: Snakemake -*-

rule download_card_db:
    output:
        str(ANNOTATION_FP/'card'/'db'/'card.json')
    params:
        db_dir = str(ANNOTATION_FP/'card'/'db'),
        bz2_name = str(ANNOTATION_FP/'card'/'db.tar.bz2')
    shell:
        """
        wget https://card.mcmaster.ca/latest/data -O {params.bz2_name}
        tar -xvjf {params.bz2_name} --directory {params.db_dir}
        rm {params.bz2_name}
        """

rule run_rgi:
    input: 
        faa = str(ANNOTATION_FP/'genes'/'prodigal'/'{sample}_genes_prot.fa'),
        rg_db_read = str(ANNOTATION_FP/'card'/'db_in.txt')
    output:
        str(ANNOTATION_FP/'card'/'{sample}_rgi.txt')
    params:
        out = str(ANNOTATION_FP/'card'/'{sample}_rgi')
    conda:
        "sbx_rgi_env.yml"
    shell:
        """
        rgi main --input_sequence {input.faa} --output_file {params.out} --input_type protein --low_quality --clean
        """

rule load_rgi:
    input:
        str(ANNOTATION_FP/'card'/'db'/'card.json')
    output:
        temp(str(ANNOTATION_FP/'card'/'db_in.txt'))
    conda:
        "sbx_rgi_env.yml"
    shell:
        """
        rgi load -i {input} > {output}
        """

rule all_rgi:
    input:
        expand(str(ANNOTATION_FP/'card'/'{sample}_rgi.txt'),sample=Samples.keys())

